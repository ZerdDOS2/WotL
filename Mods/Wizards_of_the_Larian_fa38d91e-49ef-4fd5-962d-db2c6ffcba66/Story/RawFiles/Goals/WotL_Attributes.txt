Version 1
SubGoalCombiner SGC_AND
INITSECTION
//DB_WotL_AttributesNames((STRING)_Vanilla, (STRING)_Attribute);
DB_WotL_AttributesNames("Strength", "Power");
DB_WotL_AttributesNames("Finesse", "Finesse");
DB_WotL_AttributesNames("Intelligence", "Accuracy");
DB_WotL_AttributesNames("Constitution", "Constitution");
DB_WotL_AttributesNames("Memory", "Memory");
DB_WotL_AttributesNames("Wits", "Wits");
KBSECTION
//REGION Initial set of the attribute variables
IF
	StoryEvent((CHARACTERGUID)_Char, "WotL_SetAllVariables")
AND
	DB_WotL_AttributesNames(_Vanilla, _Attribute)
AND
	StringConcatenate("WotL_Attribute_", _Attribute, _Variable)
AND
	NOT GetVarInteger(_Char, _Variable, _)
AND
	CharacterGetAttribute(_Char, _Vanilla, _Value)
THEN
	SetVarInteger(_Char, _Variable, _Value);
	PROC_WotL_GiveAttributeStatus(_Char, _Attribute, _Value);
//END_REGION

//REGION Attribute changed
IF
	StoryEvent((CHARACTERGUID)_Char, "WotL_SetAllVariables")
AND
	DB_WotL_AttributesNames(_Vanilla, _Attribute)
AND
	StringConcatenate("WotL_Attribute_", _Attribute, _Variable)
AND
	GetVarInteger(_Char, _Variable, _OldValue)
AND
	CharacterGetAttribute(_Char, _Vanilla, _Value)
AND
	_OldValue != _Value
THEN
	SetVarInteger(_Char, _Variable, _Value);
	PROC_WotL_GiveAttributeStatus(_Char, _Attribute, _Value);
//END_REGION

//REGION PROC to change the attribute status
PROC
	PROC_WotL_GiveAttributeStatus((CHARACTERGUID)_Char, (STRING)_Attribute, (INTEGER)_Value)
AND
	IntegertoString(_Value, _ValueSTR)
AND
	StringConcatenate("WotL_", _Attribute, _StatusPartOne)
AND
	StringConcatenate(_StatusPartOne, "_", _StatusPartTwo)
AND
	StringConcatenate(_StatusPartTwo, _ValueSTR, _Status)
THEN
	NOT DB_WotL_BlacklistVariableStatuses(_Status);
	DB_WotL_BlacklistVariableStatuses(_Status);
	ApplyStatus(_Char, _Status, -1.0, 1);
	DebugBreak(_Status);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "__WotL_Goal"
