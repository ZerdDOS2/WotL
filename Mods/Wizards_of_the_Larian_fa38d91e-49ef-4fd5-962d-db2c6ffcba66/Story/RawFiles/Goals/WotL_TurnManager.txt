Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION DB_WotL_CurrentTurn keeps track of the current turns character
// Adds a character to the DB at turn start
IF
    ObjectTurnStarted((CHARACTERGUID)_Char)
AND
    CombatGetIDForCharacter(_Char, _ID)
AND
    NOT DB_WotL_CurrentTurn(_Char, _ID)
THEN
    DB_WotL_CurrentTurn(_Char, _ID);

IF
	ObjectTurnStarted((CHARACTERGUID)_Char)
THEN
	// Calls the Leadership Turn Start function (WotL_AbilitiesEffects.lua)
    NRD_LuaCall("WotL_LeadershipTurn", (STRING)_Char);

// Removes a character from the DB at turn end
IF
    ObjectTurnEnded((CHARACTERGUID)_Char)
AND
    CombatGetIDForCharacter(_Char, _ID)
AND
    DB_WotL_CurrentTurn(_Char, _ID)
THEN
    NOT DB_WotL_CurrentTurn(_Char, _ID);
    // Iterates all Statuses currently posessed by the character
    NRD_IterateStatuses(_Char, "WotL_RemoveStatusesAtTurnEnd");
	
IF
	ObjectTurnEnded((CHARACTERGUID)_Char)
THEN
	// Calls the Leadership Turn End function (WotL_AbilitiesEffects.lua)
    NRD_LuaCall("WotL_LeadershipEndTurn", (STRING)_Char);
//END_REGION

//REGION Status Request Delete
// Removes statuses requested at turn end (WotL_StatusRequestDelete.lua)
IF
    NRD_StatusIteratorEvent("WotL_RemoveStatusesAtTurnEnd", _Char, _Status, _Handle)
THEN
    NRD_LuaCall("WotL_RemoveStatusesAtTurnEnd", (STRING)_Char, _Status, (STRING)_Handle);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "__WotL_Goal"
