Version 1
SubGoalCombiner SGC_AND
INITSECTION
//DB_WotL_AbilityGainLevels((INTEGER)_Level);
DB_WotL_AbilityGainLevels(3);
DB_WotL_AbilityGainLevels(4); // Even level bonus
DB_WotL_AbilityGainLevels(5);
DB_WotL_AbilityGainLevels(6); // Even level bonus
DB_WotL_AbilityGainLevels(7);
DB_WotL_AbilityGainLevels(9);
DB_WotL_AbilityGainLevels(10); // Even level bonus
DB_WotL_AbilityGainLevels(11);
DB_WotL_AbilityGainLevels(13);
DB_WotL_AbilityGainLevels(14); // Even level bonus
DB_WotL_AbilityGainLevels(15);
DB_WotL_AbilityGainLevels(17);
DB_WotL_AbilityGainLevels(19);
DB_WotL_AbilityGainLevels(20); // Even level bonus
DB_WotL_AbilityGainLevels(21);
DB_WotL_AbilityGainLevels(23);
DB_WotL_AbilityGainLevels(25);
DB_WotL_AbilityGainLevels(27);
DB_WotL_AbilityGainLevels(29);
DB_WotL_AbilityGainLevels(30); // Even level bonus
DB_WotL_AbilityGainLevels(31);
DB_WotL_AbilityGainLevels(33);
DB_WotL_AbilityGainLevels(35);
KBSECTION
// I decided to make the logic more robust to allow compatibility with the points gained
// even if leveled up by Cheat Commander or something similar

//REGION Give Ability Points
IF
	CharacterLeveledUp(_Char)
AND
	NOT DB_WotL_LevelUpBlacklist(_Char)
AND
	GetVarInteger(_Char, "WotL_Level", _OldLevel)
AND
	CharacterGetLevel(_Char, _Level)
AND
	DB_WotL_AbilityGainLevels(_PointLevel)
AND
	_PointLevel > _OldLevel
AND
	_PointLevel <= _Level
THEN
	CharacterAddAbilityPoint(_Char, 1);
//END_REGION

//REGION Update Level Var
IF
	CharacterLeveledUp(_Char)
AND
	GetVarInteger(_Char, "WotL_Level", _)
THEN
	DB_WotL_LevelUpBlacklist(_Char);
	ProcObjectTimer(_Char, "WotL_UpdateLevelVar", 1000);

PROC
	ProcObjectTimerFinished((CHARACTERGUID)_Char, "WotL_UpdateLevelVar")
AND
	CharacterGetLevel(_Char, _Level)
THEN
	NOT DB_WotL_LevelUpBlacklist(_Char);
	SetVarInteger(_Char, "WotL_Level", _Level);
//END_REGION

//REGION Initial setup
IF
	StoryEvent((CHARACTERGUID)_Char, "WotL_SetAllVariables")
AND
	ObjectGetFlag(_Char, "WotL_CharacterInitialized", 1)
AND
	NOT GetVarInteger(_Char, "WotL_Level", _)
AND
	CharacterGetLevel(_Char, _Level)
THEN
	SetVarInteger(_Char, "WotL_Level", _Level);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "__WotL_Goal"
